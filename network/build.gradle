
plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id "maven-publish"
    id "com.jfrog.artifactory"
}

ext {
    network_version = "1.0.0"
}

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.minSDKVersion
        targetSdkVersion rootProject.targetSDKVersion
        versionName network_version
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}
artifactory {
    contextUrl = 'https://mobilesdk.jfrog.io/artifactory/'
    publish {
        repository {
            repoKey = "adsdk"
            username = "sdk-dev@ad.tech"
            password = "Androiddev1"
        }
    }
}

project.afterEvaluate {
    artifactoryPublish.dependsOn("assemble")
    publishing {
        publications {
            aar(MavenPublication) {
                setGroupId 'com.adtech.net'
                //You can either define these here or get them from project conf elsewhere
                setArtifactId 'network'
                version network_version
                artifact bundleReleaseAar //aar artifact you want to publish

                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.implementation.allDependencies.each {
                        if(it.name != "logger") {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                        }
                    }
                }
            }
        }
    }
    artifactoryPublish {
        publications(publishing.publications.aar)
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$project.kotlin_version"
    implementation "androidx.core:core-ktx:$core_ktx"
    api "com.squareup.retrofit2:retrofit:$retrofit"
    api "com.squareup.okhttp3:logging-interceptor:$okhttp3"
    api "com.squareup.retrofit2:converter-scalars:$retrofit"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinx_coroutines"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit"
    implementation project(':logger')
}